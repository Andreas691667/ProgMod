class RobotController

instance variables
    private ctl_qt : real;   -- target angle
    private ctl_q : real;    -- current angle (radians)
    private ctl_dq : real;   -- derivative (velocity)
    private K : real := 0; -- proportionality coefficient for updating velocity. Public for debugging needs

    private ctl_data_history : seq of (real*real*real*real*real);


values
    N1 : real = 0;          -- lower bound //operation range
    N2 : real = 3.14/2;     -- upper bound //operation range

    M1 : real = -10*3.14 / 180;         -- lower safety bound
    M2 : real = (5*3.14)/9;             -- upper safety bound
    alpha : real = 2 * 3.14 / 180;      -- 2 degrees accept margin when near target

operations
    public RobotController: () ==> RobotController
    RobotController() == (
        
        -- initilialize variables
        ctl_dq :=0;
        ctl_q := 0;
        ctl_qt := 0;

        ctl_data_history := [];
    );

    -- update velocity based on error
    public UpdateVelocity : () ==> () 
    UpdateVelocity() == duration(0) (
        if (ctl_q <= M2 and ctl_q > N2) or (ctl_q >= M1 and ctl_q < N1)
        then 
            K := 0.1
        else if ((ctl_q <= N2 and ctl_q > (ctl_qt+alpha)) or (ctl_q >= N1 and ctl_q < (ctl_qt - alpha)))
        then
            K := 0.20
        else
            K := 0;

        ctl_dq := (ctl_qt-ctl_q) * K; 
        MySystem`env.SetVelocity(ctl_dq);            -- change in environment is instantaneous

        ctl_data_history := ctl_data_history ^ [mk_(time, ctl_qt, ctl_q, ctl_dq, K)];
    );

    -- set target angle method 
    public SetTargetAngle : (real) ==> () 
    SetTargetAngle(theta) == duration(0) (
        ctl_qt := theta;
    )
    pre theta >= N1 and theta <= N2;

    -- Update variables from the environment. Duration probably not 0. 
    public MonitorArm : () ==> ()
    MonitorArm() == duration(0) (
        ctl_q := MySystem`env.GetPosition();
    );

    -- getter functions. at this point these are made pure to be able to call them in precondition of step
    public pure GetPosition : () ==> real 
    GetPosition() == (
        return ctl_q;
    );

    public pure GetVelocity : () ==> real 
    GetVelocity() == (
        return ctl_dq;
    );

    public pure GetTarget : () ==> real 
    GetTarget() == (
        return ctl_qt;
    );

    public GetDataHistory : () ==> seq of (real*real*real*real*real)
    GetDataHistory() == (
        return ctl_data_history;
    );

end RobotController